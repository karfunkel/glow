apply plugin: 'groovy'

group = "org.aklein.glow"
version = "0.1"
description = "An awesome groovy library"

repositories {
    jcenter()
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

// code below this can be safely removed if you don't want publish library ever

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin:"com.jfrog.bintray"
apply plugin: 'maven-publish'

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {

    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['groovyMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven' // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        userOrg = 'bintray_user'
        desc = projectDescription
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
