apply plugin: 'groovy'

repositories {
    jcenter()
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

// code below this can be safely removed if you don't want publish library ever

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}

apply plugin: "com.jfrog.bintray"
apply plugin: 'maven-publish'

def projectName = project.name
def projectDescription = project.description

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java
            groupId group
            artifactId projectName
            version version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    pkg {
        repo = 'glow'           // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        userOrg = 'karfunkel'
        desc = projectDescription
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/karfunkel/glow.git'
    }

    publications = ['groovyMaven']
    dryRun = false // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
